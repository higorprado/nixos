{ ... }:
{
  flake.modules.homeManager.dev-devenv =
    { pkgs, ... }:
    let
      devenvTemplatesFlake = pkgs.runCommandLocal "devc-templates-flake" { } ''
        mkdir -p "$out/config/devenv-templates"
        cp -r ${../../../config/devenv-templates}/. "$out/config/devenv-templates/"

        {
          echo '{'
          echo '  description = "Embedded devenv templates for devc";'
          echo '  outputs = { self }: {'
          echo '    templates = {'

          for dir in ${../../../config/devenv-templates}/*; do
            name="$(basename "$dir")"
            [ -d "$dir" ] || continue
            printf '      %s = { path = ./config/devenv-templates/%s; description = "devenv project template (%s)"; };\n' "$name" "$name" "$name"
          done

          echo '      default = { path = ./config/devenv-templates/python; description = "devenv project template (python)"; };'
          echo '    };'
          echo '  };'
          echo '}'
        } > "$out/flake.nix"
      '';
      devc = pkgs.writeShellApplication {
        name = "devc";
        runtimeInputs = with pkgs; [
          coreutils
          jq
          nix
        ];
        text = ''
          usage() {
            cat <<'EOF'
          Usage:
            devc list
            devc <template> [target]
            devc help

          Examples:
            devc python new_project
            devc python .

          Environment:
            DEVC_FLAKE  Flake reference that exposes templates
                        (default: embedded tracked templates)
          EOF
          }

          if [[ $# -eq 0 ]]; then
            usage >&2
            exit 1
          fi

          case "$1" in
            help|-h|--help)
              usage
              exit 0
              ;;
          esac

          flake_ref="''${DEVC_FLAKE:-path:${devenvTemplatesFlake}}"

          if [[ "$1" == "list" ]]; then
            nix flake show "$flake_ref" --json \
              | jq -r '.templates // {} | keys[]' \
              | sort
            exit 0
          fi

          if [[ $# -gt 2 ]]; then
            echo "Error: too many arguments." >&2
            usage >&2
            exit 1
          fi

          template="$1"
          target="''${2:-.}"
          template_ref="$flake_ref#$template"

          if [[ "$target" == "." ]]; then
            exec nix flake init -t "$template_ref"
          fi

          exec nix flake new "$target" -t "$template_ref"
        '';
      };
    in
    {
      home.packages = with pkgs; [
        devenv
        cachix
        devc
      ];

      programs.direnv = {
        enable = true;
        nix-direnv.enable = true;
        config.global.hide_env_diff = true;
      };

      xdg.configFile."direnv/direnvrc".source = (
        pkgs.runCommand "devenv-direnvrc" { buildInputs = [ pkgs.devenv ]; } ''
          devenv direnvrc > $out
        ''
      );
    };
}
